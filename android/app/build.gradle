def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.google.gms.google-services'

def prodKeystoreProperties = new Properties()
   def prodKeystorePropertiesFile = rootProject.file('prod.key.properties')
   if (prodKeystorePropertiesFile.exists()) {
       prodKeystoreProperties.load(new FileInputStream(prodKeystorePropertiesFile))
   }

def previewKeystoreProperties = new Properties()
   def previewKeystorePropertiesFile = rootProject.file('prod.key.properties')
   if (previewKeystorePropertiesFile.exists()) {
       previewKeystoreProperties.load(new FileInputStream(previewKeystorePropertiesFile))
   }

android {
    compileSdkVersion 30

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.brennerferreira.boleto_organizer"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
       release {
           keyAlias prodKeystoreProperties['keyAlias']
           keyPassword prodKeystoreProperties['keyPassword']
           storeFile prodKeystoreProperties['storeFile'] ? file(prodKeystoreProperties['storeFile']) : null
           storePassword prodKeystoreProperties['storePassword']
       }

       preview {
           keyAlias previewKeystoreProperties['keyAlias']
           keyPassword previewKeystoreProperties['keyPassword']
           storeFile previewKeystoreProperties['storeFile'] ? file(previewKeystoreProperties['storeFile']) : null
           storePassword previewKeystoreProperties['storePassword']
       }
   }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            aaptOptions {
                ignoreAssetsPattern '!mlkit_pose:!mlkit_label_default_model:'
            }
        }

        profile {
            signingConfig signingConfigs.preview
            aaptOptions {
                ignoreAssetsPattern '!mlkit_pose:!mlkit_label_default_model:'
            }
        }

        debug {
            aaptOptions {
                ignoreAssetsPattern '!mlkit_pose:!mlkit_label_default_model:'
            }
        }
    }

    packagingOptions {
        exclude 'lib/**/libtranslate_jni.so'
        exclude 'lib/**/libdigitalink.so'
        exclude 'lib/**/libxeno_native.so'
        exclude 'lib/**/libmlkitcommonpipeline.so'
        // exclude 'lib/**/libbarhopper_v2.so'
        exclude 'lib/**/libclassifier_jni.so'
        exclude 'lib/**/libface_detector_v2_jni.so'
        exclude 'lib/**/libtensorflowlite_jni.so'
        exclude 'lib/**/liblanguage_id_jni.so'
    }

    flavorDimensions "flavor-type"

    productFlavors {
        dev {
            dimension "flavor-type"
            applicationIdSuffix ".dev"
            versionCode 1
            versionName "1.0"
        }

        preview {
            dimension "flavor-type"
            applicationIdSuffix ".preview"
            versionCode flutterVersionCode.toInteger()
            versionName flutterVersionName
        }

        prod {
            dimension "flavor-type"
            applicationIdSuffix ".prod"
            versionCode flutterVersionCode.toInteger()
            versionName flutterVersionName
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
}
